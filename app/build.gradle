apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.squareup.sqldelight'

android {
    defaultConfig {
        applicationId "com.uber.okbuck.example"
        multiDexEnabled true
        buildConfigField "boolean", "CAN_JUMP", "true"
        buildConfigField "boolean", "XLOG_ENABLED", "true"
        buildConfigField "int", "EXOPACKAGE_FLAGS", "0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        missingDimensionStrategy 'tier', 'free', 'paid'
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            buildConfigField "boolean", "CAN_JUMP", "true"
            applicationIdSuffix = ".dev"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
        prod {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".prod"
        }
        demo {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".demo"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_CONFIG", "true"
            applicationIdSuffix = ".debug"
        }
        release {
            buildConfigField "boolean", "CAN_JUMP", "true"
            buildConfigField "boolean", "DEBUG_CONFIG", "false"
        }
    }
}

repositories {
    flatDir {
        dirs 'aars'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api(name: 'rxscreenshotdetector-1.2.0', ext: 'aar')
    api deps.external.rxjava
    api deps.external.rxandroid
    api deps.external.rxPermissions
    api deps.support.multidex
    api deps.external.butterKnife
    api deps.support.design
    api deps.support.recyclerView
    api deps.external.leakCanary
    api deps.external.xlogAndroidIdle
    api deps.apt.autoValue

    api project(':libraries:emptylibrary')
    api project(':libraries:kotlinandroidlibrary')
    api project(':dummylibrary')

    annotationProcessor deps.apt.autoValue
    annotationProcessor deps.apt.butterKnifeCompiler
    annotationProcessor deps.apt.daggerCompiler
    annotationProcessor deps.apt.autoValueGson

    testImplementation deps.test.junit
    testImplementation deps.test.mockito
    testImplementation deps.support.annotations

    androidTestImplementation deps.test.espressoCore
    androidTestImplementation deps.test.espressoRunner
}

android.testVariants.all { variant ->
    variant.getCompileConfiguration().exclude group: 'com.android.support', module: 'support-annotations'
    variant.getRuntimeConfiguration().exclude group: 'com.android.support', module: 'support-annotations'
}

afterEvaluate {
    tasks.withType(JavaCompile) { JavaCompile javaCompile ->
        javaCompile.options.compilerArgs << "-Xlint:deprecation"
    }
}
