plugins {
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7"
    id "com.fizzed.rocker" version "0.22.0"
}

apply from: "../dependencies.gradle"

repositories {
    jcenter()
    google()
    // Remove once butterknife is updated to a stable version
    // TODO: https://github.com/uber/okbuck/issues/570
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

jar {
    exclude('**/*.rocker.raw')
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

sourceSets.main.java.srcDirs = ["src/main/rocker"]
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]
sourceSets.main.rocker.srcDirs = ["src/main/rocker", "src/main/java"]

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation deps.build.androidPlugin
    implementation deps.build.commonsIo
    implementation deps.build.commonsLang
    implementation deps.build.kotlinPlugin
    implementation deps.build.mavenArtifact
    implementation deps.build.okio
    implementation deps.build.rockerRuntime

    testImplementation deps.test.junit
}

rocker {
    discardLogicWhitespace true
    optimize true
    extendsClass "com.uber.okbuck.template.core.RuleTemplate"
    extendsModelClass "com.uber.okbuck.template.core.Rule"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// Release alpha version of the plugin and fix issues that come up. this will
// allow us to bake the plugin for a bit before doing minor bump release.
// We will release 0.33.0 after stable AGP 3.1.0 is released.
def publishVersion = '0.33.0-alpha03'
group = 'com.uber'
version = publishVersion
def siteUrl = 'https://github.com/uber/okbuck'
def gitUrl = 'https://github.com/uber/okbuck.git'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            artifactId 'okbuck'
        }
    }
}

bintray {
    user = project.properties.get("bintray.user")
    key = project.properties.get("bintray.apikey")
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'gradle-plugins'
        name = 'okbuck'
        userOrg = 'uber'
        desc = 'Gradle plugin that lets developers utilize the Buck build system on a Gradle project'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['buck', 'okbuck', 'uber']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            released = new java.text.SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = publishVersion
        }
    }
}
